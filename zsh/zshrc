setopt localoptions promptsubst

export EDITOR='code -w'
export GOPATH="$HOME/go"
export GEM_HOME="$HOME/.gem"
export DEFAULT_STABLE_RUBY="3.0.3"
export JAVA_HOME="/Applications/Android Studio.app/Contents/jbr/Contents/Home"
export ANDROID_HOME="$HOME/Library/Android/sdk"

# Load files
for file ($HOME/.dotfiles/zsh/*.zsh) source $file

REPORTTIME=10 # print elapsed time when command runs for more than 10 seconds

# make GPG stuff work

GPG_AGENT_SOCKET="$(gpgconf --list-dirs agent-ssh-socket)"
if [ ! -S $GPG_AGENT_SOCKET ]; then
  gpg-agent --daemon --pinentry-program "${BREW_PREFIX}/bin/pinentry-mac" >/dev/null 2>&1
  export GPG_TTY=$(tty)
fi

## Randoms

ulimit -n 65536

eval "$(direnv hook zsh)"

# Load NVM
export NVM_DIR="$HOME/.nvm"
[ -s "${BREW_PREFIX}/opt/nvm/nvm.sh" ] && . "${BREW_PREFIX}/opt/nvm/nvm.sh"

# Ruby
source "${BREW_PREFIX}/opt/chruby/share/chruby/chruby.sh"
source "${BREW_PREFIX}/opt/chruby/share/chruby/auto.sh"

# Set up the actual prompt
precmd() {
  setopt extendedglob

  local current_pwd="${PWD/#$HOME/~}"
  local ret_directory

  # Split path components
  local pcs=("${(@s:/:)current_pwd}")
  # Length of array of path components
  local pcs_len="${#pcs[@]}"

  # Number of full path components you want at the end of the path
  local full_pc_len=2

  if [[ "${current_pwd}" == (#m)[/~] ]]; then
    ret_directory="$MATCH"
    unset MATCH
  elif [ ${#pcs[@]} -ge $((full_pc_len + 1)) ]; then
    local path_full_comps=$((pcs_len - full_pc_len))
    local short_path_comps="${(@j:/:M)${pcs[@]:0:$((path_full_comps))}##.#?}"
    local full_path_comps="/${${(@j:/:)${pcs[@]:$((pcs_len - full_pc_len)):$((full_pc_len))}}//\%/%%}"
    ret_directory="${short_path_comps}${full_path_comps}"
  else
    ret_directory="${current_pwd}"
  fi

  unset current_pwd

  vcs_info
  PROMPT="%* ${ret_directory}$(prompt_git) %F{magenta}‚ùØ %f"
}
